def hasGTest = file("/usr/local/Cellar/gtest/include").exists()

model {
    repositories {
        libs(PrebuiltLibraries) {
            // TODO dynamically download, brew install cunit, then use gradlew check
            if (hasGTest) {
                gtest {
                    headers.srcDir "/usr/local/Cellar/gtest/include"

                    binaries.withType(StaticLibraryBinary) {
                        staticLibraryFile = file("/usr/local/Cellar/gtest/libgtest.a")
                    }
                }
            }
        }
    }

    components {
        crackingTheCodeCpp(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDir "src/main/cpp"
                        include "**/*.cpp"
                    }

                    exportedHeaders {
                        srcDir "src/main/cpp/include"
                    }
                }
            }
        }
    }

    testSuites {
        if (hasGTest) {
            crackingTheCodeCppTest(GoogleTestTestSuiteSpec) {
                sources {
                    cpp {
                        source {
                            srcDir "src/test/cpp"
                            include "**/*.cpp"
                        }

                        exportedHeaders {
                            srcDir "src/test/cpp/include"
                        }
                    }
                }

                testing $.components.crackingTheCodeCpp
            }
        }
    }

    binaries {
        all {
            if (toolChain in Gcc || toolChain in Clang) {
                cCompiler.args "-Wall", "-Wextra", "-Werror", "-O3"
                cppCompiler.args "-std=c++14", "-Wall", "-Wextra", "-Werror", "-O3"
            }

            if (toolChain in VisualCpp) {
                cCompiler.args "/Wall", "/Wx"
                cppCompiler.args "/Wall", "/Wx"
            }
        }

        if (hasGTest) {
            withType(GoogleTestTestSuiteBinarySpec) {
                lib library: "gtest", linkage: "static"
            }
        }
    }
}
