buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
    }

    dependencies {
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.12.0"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" } // Mirrors jcenter() and mavenCentral()
}

apply plugin: "c"
apply plugin: "cunit-test-suite"
apply plugin: "cpp"
apply plugin: "google-test-test-suite"
apply plugin: "pmd"
apply plugin: "java"
apply plugin: "groovy"
apply plugin: "jacoco"
apply plugin: "findbugs"
apply plugin: "checkstyle"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.kt3k.coveralls"
apply plugin: "com.github.ben-manes.versions"

tasks.withType(CCompile) {
}

tasks.withType(CppCompile) {
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    // Show all warnings except boot classpath
    configure(options) {
        compilerArgs << "-Xlint:all"            // Turn on all warnings
        compilerArgs << "-Xlint:-options"       // Turn off "missing" bootclasspath warning
        compilerArgs << "-Xlint:-processing"    // Turn off warning about not claiming annotations
        compilerArgs << "-Werror"               // Turn warnings into errors
        compilerArgs << "-Xdiags:verbose"       // Turn on verbose errors
        encoding = "utf-8"
    }
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    // Show all warnings except boot classpath
    configure(options) {
        compilerArgs << "-Xlint:all"            // Turn on all warnings
        compilerArgs << "-Xlint:-options"       // Turn off "missing" bootclasspath warning
        compilerArgs << "-Xlint:-processing"    // Turn off warning about not claiming annotations
        compilerArgs << "-Werror"               // Turn warnings into errors
        compilerArgs << "-Xdiags:verbose"       // Turn on verbose errors
        encoding = "utf-8"
    }

    configure(groovyOptions) {
        encoding = "utf-8"
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        events = ["failed", "skipped"]
    }
}

tasks.withType(JavaForkOptions) {
    // Prevent forked processes from stealing focus (on MacOS at least)
    jvmArgs "-Djava.awt.headless=true"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["codeeval/**",
                              "crackingthecode/part3knowledgebased/**",
                              "**/Chapter7ObjectOrientedDesign**",
                              "**/Chapter11Testing**",
                              "**/Chapter12SystemDesignAndMemoryLimits**",
                              "projecteuler/**"])
        })
    }
}

findbugs {
    effort = "max"
    reportLevel = "high"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            // TODO dynamically download, brew install cunit, then use gradlew check
            cunit {
                headers.srcDir "/usr/local/Cellar/cunit/2.1-3/include"

                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("/usr/local/Cellar/cunit/2.1-3/lib/libcunit.a")
                }
            }

            googleTest {
                headers.srcDir "/usr/local/Cellar/gtest/include/"

                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("/usr/local/Cellar/gtest/libgtest.a")
                }
            }
        }
    }

    components {
        crackingthecode_c(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir "src/main/c/crackingthecode"
                        include "**/*.c"
                    }

                    exportedHeaders {
                        srcDir "src/main/c/crackingthecode/include"
                    }
                }
            }
        }

        crackingthecode_cpp(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDir "src/main/cpp/crackingthecode"
                        include "**/*.cpp"
                    }

                    exportedHeaders {
                        srcDir "src/main/cpp/crackingthecode/include"
                    }
                }
            }
        }
    }

    testSuites {
        crackingthecode_cTest(CUnitTestSuiteSpec) {
            sources {
                c {
                    source {
                        srcDir "src/test/c/crackingthecode"
                        include "**/*.c"
                    }

                    exportedHeaders {
                        srcDir "src/test/c/crackingthecode/include"
                    }
                }
            }

            testing $.components.crackingthecode_c
        }

        crackingthecode_cppTest(GoogleTestTestSuiteSpec) {
            sources {
                cpp {
                    source {
                        srcDir "src/test/cpp/crackingthecode"
                        include "**/*.cpp"
                    }

                    exportedHeaders {
                        srcDir "src/test/cpp/crackingthecode/include"
                    }
                }
            }

            testing $.components.crackingthecode_cpp
        }
    }

    binaries {
        withType(CUnitTestSuiteBinarySpec) {
            lib library: "cunit", linkage: "static"
        }

        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static"
        }
    }
}

dependencies {
    compile "com.google.code.findbugs:annotations:3.0.1"

    testCompile localGroovy()
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4", { exclude module: "groovy-all" }
    testCompile "nl.jqno.equalsverifier:equalsverifier:2.1"
    testCompile "cglib:cglib-nodep:3.2.2"

    checkstyle 'com.puppycrawl.tools:checkstyle:6.19'
}
